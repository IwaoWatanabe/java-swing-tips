buildscript {
println "gradle version: " + gradle.gradleVersion
println "JVM: " + System.getProperty("java.home") + " arch: " + System.getProperty("os.arch")
// ビルドに利用しているJVM情報を表示

repositories {
  mavenLocal()
  repositories {
      maven { url "https://plugins.gradle.org/m2/" }
  }
  maven {
    url 'https://pkgs.dev.azure.com/iwaowatanabe/kt-learning/_packaging/OldVM/maven/v1'
    // see: https://dev.azure.com/iwaowatanabe/kt-learning/_artifacts/feed/OldVM
  }
  mavenCentral()
}
dependencies {
  classpath 'org.gretty:gretty:2.3.1'
  if (java.lang.Integer.getInteger("java.specification.version") < 1.8) {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  }
  else if (gradle.gradleVersion < '7.0') {
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
  }
  else {
    classpath 'com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:7.1.2'
  }
}}

def sourceEncoding = 'utf-8'
def documentEncoding = 'utf-8'
def env = System.env
def gver = gradle.gradleVersion
def sp = System.properties
def osname = sp['os.name'].toLowerCase()
def osarch = sp['os.arch']
def userHome = sp['user.home']
def gverinfo = "Gradle ${gradle.gradleVersion} (${sp['os.name']}:${sp['os.arch']}:${sp['os.version']})"

allprojects {
apply plugin: 'java'
sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['test']
if (gver < '5.0') {
  sourceSets.main.output.classesDir = 'build/classes'
  sourceSets.main.output.resourcesDir = 'build/resources'
  sourceSets.test.output.classesDir = 'build/test-classes'
  sourceSets.test.output.resourcesDir = 'build/test-resources'
}
repositories {
  mavenLocal()
  maven {
    url 'https://pkgs.dev.azure.com/iwaowatanabe/kt-learning/_packaging/OldVM/maven/v1'
    // see: https://dev.azure.com/iwaowatanabe/kt-learning/_artifacts/feed/OldVM
  }
  mavenCentral()
}
tasks.withType(JavaCompile) {
  // options.compilerArgs << '-Xlint:unchecked,divzero'
  options.deprecation = false
  options.encoding = sourceEncoding
  doFirst {
    def bootClassPath = ''
    if (sourceCompatibility == '1.8' && env.JRE8_HOME != null)
        bootClassPath ="${env.JRE8_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.7' && env.JRE7_HOME != null)
        bootClassPath = "${env.JRE7_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.6' && env.JRE6_HOME != null)
        bootClassPath = "${env.JRE6_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.5' && env.JRE5_HOME != null)
        bootClassPath = "${env.JRE5_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.4' && env.JRE4_HOME != null)
        bootClassPath = "${env.JRE4_HOME}/lib/rt.jar"
    else if (sourceCompatibility == '1.3' && env.JRE3_HOME != null)
        bootClassPath = "${env.JRE3_HOME}/lib/rt.jar"

    if (bootClassPath != '') {
        println "INFO: bootstrapClasspath: ${bootClassPath}"
        options.fork = true
        if (gver < '5.0')
          options.bootClasspath = "${bootClassPath}"
      // ↑ for gradle 4.x with JDK7
        else
          options.bootstrapClasspath = files("${bootClassPath}")
      // ↑ for gradle 5.x with JDK8 or later
    }
  }
}
javadoc {
  source = sourceSets.main.allJava
  classpath = sourceSets.main.compileClasspath
  options.charSet = documentEncoding
  options.encoding = sourceEncoding
  if (env.DEBUG != null) options.memberLevel = JavadocMemberLevel.PACKAGE
  options.addStringOption('Xdoclint:none', '-quiet')
  doLast {
    println "INFO: open ${project.docsDir}/javadoc/index.html"
  }
}

task zipdoc(type: Zip, dependsOn: javadoc) {
  description 'javadoc を zipでアーカイブする'
  if (gver < '7.0') {
    destinationDir = file('../apidocs')
    classifier = "apidoc-" + new Date().format('yyyy-MMdd')
    extension = 'zip'
  }
  else {
    destinationDirectory = file('../apidocs')
    archiveClassifier = "apidoc-" + new Date().format('yyyy-MMdd')
    archiveExtension = 'zip'
  }
  from "${project.docsDir}/javadoc"
  excludes = ['build', '.*']
  doLast {
    println "INFO: created: ${archivePath}"
  }
}

task copylib(type: Copy) {
  description '依存するライブラリを収集して lib フォルダに格納する'
// from (configurations.compile + configurations.testCompile)
  from configurations.runtimeClasspath
  into "lib" 
}}

subprojects {
  // sourceCompatibility = 1.6

task hotDeploy {
  dependsOn "ear"
  doFirst {
    println "from :build/libs/${project.ear.archiveName} To: ${env.DEPLOY_PATH}"
  }
  doLast {
    copy {
      from "build/libs/${project.ear.archiveName}"
      into "${env.DEPLOY_PATH}"
    }
  }
}}

project(':tips4') {
description 'swing tips for jre1.4'
sourceCompatibility = '1.4'
// mainClassName = 'tips.Launcher'
jar {
  manifest {
      // attributes 'Main-Class': mainClassName
  }
}
dependencies{
  testCompile group: 'junit', name: 'junit', version: '4.5'
}}

project(':tips5') {
description 'swing tips for jre5'
sourceCompatibility = '1.5'
// mainClassName = 'tips.Launcher'
jar {
  manifest {
      // attributes 'Main-Class': mainClassName
  }
}
dependencies{
  // compileOnly files('../lib/jnlp-jre5.jar')
  implementation 'javax.jnlp:jnlp:1.2'
  testCompile group: 'junit', name: 'junit', version: '4.5'
}}


project(':tips6') {
description 'swing tips for jre5'
sourceCompatibility = '1.6'
// mainClassName = 'tips.Launcher'
jar {
  manifest {
      // attributes 'Main-Class': mainClassName
  }
}
javadoc {
  options.source = "1.6"
}
dependencies{
  testCompile group: 'junit', name: 'junit', version: '4.5'
}}

